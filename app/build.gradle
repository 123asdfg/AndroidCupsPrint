apply plugin: 'com.android.application'

allprojects {
    afterEvaluate { project ->
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        def signingPropertiesFilePath = properties.getProperty('signingPropertiesFilePath')

        def propsFile = file(signingPropertiesFilePath)
        def configName = 'release'
        if (propsFile.exists() && android.signingConfigs.hasProperty(configName)) {
            def props = new Properties()
            props.load(new FileInputStream(propsFile))
            android.signingConfigs[configName].storePassword = props['storePassword']
            android.signingConfigs[configName].keyAlias = props['keyAlias']
            android.signingConfigs[configName].keyPassword = props['keyPassword']
        }
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    defaultConfig {
        applicationId "io.github.benoitduffez.cupsprint"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 4
        versionName "1.2"
    }

    signingConfigs {
        release {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            def signingKeystoreFilePath = properties.getProperty('signingKeystoreFilePath')

            storeFile = file(signingKeystoreFilePath)
            storePassword = ''
            keyAlias = ''
            keyPassword = ''
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    useLibrary 'org.apache.http.legacy'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.1.1'
}
